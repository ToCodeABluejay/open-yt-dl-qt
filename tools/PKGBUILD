# Maintainer: Gabriel Bauer <gabeb1277@gmail.com>

pkgname=open-yt-dl-qt-git
_gitname=open-yt-dl-qt
pkgver=0.7
pkgrel=1
pkgdesc="Open video downloader for a variety of popular websites"
arch=('any')
url="https://github.com/ToCodeABluejay/open-yt-dl-qt"
license=('GPL3')
depends=('qt5-base' 'yt-dlp' 'hicolor-icon-theme')
makedepends=('git' 'make')
optdepends=('ffmpeg: for some youtube-dl functionality')
provides=('open-yt-dl-qt')
source=("git+https://github.com/ToCodeABluejay/open-yt-dl-qt.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  echo "$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
	cd "$srcdir/$_gitname"
	qmake -config release
	make
}

package() {
	cd "$srcdir/$_gitname"
	make INSTALL_ROOT="$pkgdir/" install
}
